// Phase 3: Population cage experiment - True Overdominance Alternative
initialize() {
    defineConstant("BASE_DIR", "/scratch/cqh6wn/AOD_slim/True_overdominance");
    if (!exists("jobID")) defineConstant("jobID", 1);
    defineConstant("REP_ID", jobID);
    defineConstant("OUTPUT_FILE", BASE_DIR + "results/true_overdominance_rep" + REP_ID + ".csv");

    // Simulation Parameters
    defineConstant("K_CAGE", 1000);        // Carrying capacity 
    defineConstant("NUM_GEN", 100);        // Cage duration 
    defineConstant("N_FOUNDERS_LOW", 4);   // LF treatment
    defineConstant("N_FOUNDERS_HIGH", 20); // MF treatment
    defineConstant("LOG_INTERVAL", 1);
    defineConstant("L", 23e6);             // Chromosome 2L length 
    defineConstant("REC_RATE", 2e-8);      // Recombination rate
    defineConstant("INV_START", 2e6);
    defineConstant("INV_END", 13e6);
    defineConstant("INV_MARKER_POS", integerDiv(INV_START + INV_END, 2));

    setSeed(getSeed() + REP_ID);
    initializeSLiMModelType("nonWF");

    // m3: The Inversion Marker
    // Direct selection is handled via fitness() callback below 
    initializeMutationType("m3", 0.5, "f", 0.0); 
    m3.convertToSubstitution = F;

    initializeGenomicElementType("g1", m3, 1.0);
    initializeGenomicElement(g1, 0, L-1);
    initializeMutationRate(0.0);
    initializeRecombinationRate(REC_RATE);
}

// Setup cages
1 early() {
    sim.addSubpop("p1", 100); 
    for (ind in p1.individuals) {
        ind.genomes[0].addNewMutation(m3, 0.0, INV_MARKER_POS);
    }

    defineConstant("START_GEN", sim.cycle);
    sim.addSubpop("p2", 0); // Low-founder (LF)
    sim.addSubpop("p3", 0); // Multi-founder (MF)

    // Cages with heterozygotes
    p2.takeMigrants(sample(p1.individuals, N_FOUNDERS_LOW));
    p3.takeMigrants(sample(p1.individuals, N_FOUNDERS_HIGH));

    sim.killIndividuals(p1.individuals);
    p1.removeSubpopulation();

    // Initialize CSV 
    header = "rep,gen,treatment,inv_freq,f_std,f_het,f_inv";
    writeFile(OUTPUT_FILE, header, append=F);

    community.rescheduleScriptBlock(s1, start=sim.cycle + 1, end=sim.cycle + NUM_GEN);
    community.rescheduleScriptBlock(s2, start=sim.cycle, end=sim.cycle + NUM_GEN);
}

// Reproduction
reproduction(p2) {
    for (i in 1:K_CAGE) {
        parents = p2.sampleIndividuals(2);
        p2.addCrossed(parents[0], parents[1]);
    }
    self.active = 0;
}
reproduction(p3) {
    for (i in 1:K_CAGE) {
        parents = p3.sampleIndividuals(2);
        p3.addCrossed(parents[0], parents[1]);
    }
    self.active = 0;
}

// Recombination suppression 
recombination() {
    if (haplosome1.containsMarkerMutation(m3, INV_MARKER_POS) ==
        haplosome2.containsMarkerMutation(m3, INV_MARKER_POS)) {
        return F;
    } else {
        inInv = (breakpoints > INV_START) & (breakpoints < INV_END);
        if (!any(inInv)) return F;
        breakpoints = breakpoints[!inInv];
        return T;
    }
}

s1 2 early() {
    if (sim.cycle > START_GEN + NUM_GEN) sim.simulationFinished();
    p2.fitnessScaling = K_CAGE / p2.individualCount;
    p3.fitnessScaling = K_CAGE / p3.individualCount;
}

s2 2 late() {
    gensSinceStart = sim.cycle - START_GEN;

    if (gensSinceStart % LOG_INTERVAL == 0) {
        populations = c(p2, p3);
        labels = c("LF", "MF");

        for (i in seqAlong(populations)) {
            pop = populations[i];
            label = labels[i];
            n = pop.individualCount;

            if (n > 0) {
                m3_counts = pop.individuals.countOfMutationsOfType(m3);
                f_std = sum(m3_counts == 0) / n;
                f_het = sum(m3_counts == 1) / n;
                f_inv = sum(m3_counts == 2) / n;
                inv_freq = sum(m3_counts) / (2.0 * n);

                line = REP_ID + "," + gensSinceStart + "," + label + "," + inv_freq + "," + f_std + "," + f_het + "," + f_inv;
                writeFile(OUTPUT_FILE, line, append=T);
            }
        }
    }
}
